***************************************************
**Solar controller
**************************************************

***************************************************************************
** This controller rules the activation of the pump of the solar collectors.
** Two criteria must be matched at the same time:
**      1. Radiation must be higher than 50 W/m2
**      2. Outlet temperature of the collectors must be higher than
**      MAX(Return temperature from the grid, bottom temperature of the storage).
**      A deadband controller is introduced to make it smoother.
***************************************************************************


***********************************
** Inputs from other ddcks
***********************************

EQUATIONS #
THigh = $CollTOut
TLow = MAX($TNetReturn, $TTesBottom)
TMon = $CollTOut

***********************************
** Outputs to other ddcks
***********************************

EQUATIONS #
puPriOn = puColOnRad * puColOnTemp ! Radiation + temperature control

***********************************
** Deadband control
***********************************

CONSTANTS #
DtOn = 15       ! Upper dead band collector pump ON
DtOff = -10     ! Upper dead band collector pump OFF
TMonMax = 150   ! Maximum monitored temperature

UNIT 55 TYPE 2
PARAMETERS 2
5               ! 1 No. of oscillations
TMonMax         ! 2 High limit cut-out
INPUTS 6
THigh           ! 1 Upper input temperature Th
TLow            ! 2 Lower input temperature Tl
TMon            ! 3 Monitoring temperature Tin
puColOnTemp     ! 4 70,1 Input control function
!puColOnPri      ! 4 70,1 Input control function
DtOn            ! 5 Upper dead band dT
DtOff           ! 6 Lower dead band dT
0.0 20.0 0.0 0 DtOn DtOff

EQUATIONS #
puColOnRad = GT($IT_H,50*3.6) ! Radiation control
puColOnTemp = [55,1] ! Temperature control

*************************************
********** Online Plotter ***********
*************************************

!UNIT 501 TYPE 65     ! Online Plotter HX
!PARAMETERS 12
!#                   ! 1 Nb. of left-axis variables
!#                   ! 2 Nb. of right-axis variables
!0                   ! 3 Left axis minimum
!200                 ! 4 Left axis maximum -
!0                   ! 5 Right axis minimum
!2                   ! 6 Right axis maximum
!$nPlotsPerSim       ! 7 Number of plots per simulation
!12                  ! 8 X-axis gridpoints
!1                   ! 9 Shut off Online w/o removing
!-1                  ! 10 Logical unit for output file
!0                   ! 11 Output file units
!0                   ! 12 Output file delimiter
!INPUTS #
!THigh TLow TMon TMonMax | puColOnRad puColOnTemp puPriOn
!THigh TLow TMon TMonMax | puColOnRad puColOnTemp puPriOn
!LABELS  3
!Temperatures
!MassFlows
!Solar_control


